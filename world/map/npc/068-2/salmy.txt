068-2,81,22,0|script|Salmi|612
{
    set @equipedtorso, getequipid(equip_torso);
    set @equipedgloves, getequipid(equip_gloves);
    set @equipedshoes, getequipid(equip_shoes);
    set @equipedlegs, getequipid(equip_legs);

    set @torsoitem, @equipedtorso;
    set @glovesitem, @equipedgloves;
    set @shoesitem, @equipedshoes;
    set @legsitem, @equipedlegs;

    set @torso, 0;
    set @gloves, 0;
    set @shoes, 0;
    set @legs, 0;


// message strcharinfo(0), "Torso: " + @torsoitem + " Gloves: " + @glovesitem + " Shoes: " + @shoesitem + " Legs: " + @legsitem;

    if (@torsoitem == 755) ||
       (@torsoitem == 8161) ||
       (@torsoitem == 8164) ||
       (@torsoitem == 8167) ||
       (@torsoitem == 8170) ||
       (@torsoitem == 8173) ||
       (@torsoitem == 8188) ||
       (@torsoitem == 8179) ||
       (@torsoitem == 8182) ||
       (@torsoitem == 8185) ||
       (@torsoitem == 3230)   set @torso, 1;

    if (@legsitem == 731) ||
       (@legsitem == 8162) ||
       (@legsitem == 8165) ||
       (@legsitem == 8168) ||
       (@legsitem == 8171) ||
       (@legsitem == 8174) ||
       (@legsitem == 8177) ||
       (@legsitem == 8180) ||
       (@legsitem == 8183) ||
       (@legsitem == 8186) ||
       (@legsitem == 3229)   set @legs, 1;

    if (@shoesitem == 757)  set @shoes, 1;

    if (@glovesitem == 756) ||
       (@glovesitem == 8163) ||
       (@glovesitem == 8166) ||
       (@glovesitem == 8169) ||
       (@glovesitem == 8172) ||
       (@glovesitem == 8175) ||
       (@glovesitem == 8178) ||
       (@glovesitem == 8181) ||
       (@glovesitem == 8184) ||
       (@glovesitem == 8187) ||
       (@glovesitem == 3227)   set @gloves, 1;

     if (@gloves + @shoes +  @legs + @torso == 4) goto L_Main;

     goto L_Close3;

    

L_Main:
    emotion EMOTE_TONGUE, strcharinfo(0);
    mes "[Salmi]";
    mes "\"Greetings, Assassin. With what may I help you?\"";
    menu
        "Greetings, are you the assassin store merchant?", L_ask_salmi,
        "But what are dyes and what purpose do they serve?", L_ask_dye,
        "Can you dye something for me?", L_clothes_choice,
        "Can you also remove color from clothing?", L_ask_bleach,
        "Can you change my appearance?", L_barber,
        "Never mind.", L_Close;

L_barber:
    callfunc "Barber";
    goto L_Main;

L_ask_bleach:
    mes "[Salmi]";
    mes "\"Lol, no!";
    mes "For that, you would need to visit our brother that is close to Candide in the Tulimshar marketplace. He works there undercover and he should see that you have your Assassin Boots fitted or he will not pay attention.\"";
    goto L_Main;

L_ask_salmi:
    mes "[Salmi]";
    mes "\"That is an honest mistake you have made.";
    mes "However I am not the merchant, but rather an Assassin Master Dyer here to serve the good assassins of this island.";
    mes "I dye assassin things, upon request.\"";
    menu
        "But what are dyes and where can I get them?", L_ask_dye,
        "Can you dye assassin items for me?", L_clothes_choice,
        "Can you also remove color from clothing?", L_ask_bleach,
        "Good bye.", L_Close;

L_ask_dye:
    mes "[Salmi]";
    mes "\"Dyes are special liquids used to add color to clothing. If you want me to dye something for you you will have to bring me some dye first.";
    mes "Those behind me are nearly empty and won't last for your item.\"";
    menu
        "What do you mean, `bring you dye'?", L_explain_dye,
        "Where would I get dye?", L_explain_dye,
        "Good bye.", L_Close;

L_clothes_choice:
    cleararray @items$, "", getarraysize(@items$);
    cleararray @item_names$, "", getarraysize(@item_names$);
    set @r, 0;
    set @j, getarraysize($@DYE_assassin_items$);
    goto L_CloneArray; // clone the array from DyeConfig

L_CloneArray:
    set @items$[@r],      $@DYE_assassin_items$[@r];
    set @item_names$[@r], $@DYE_assassin_item_names$[@r];
    set @r, @r + 1;
    if(@r < @j) goto L_CloneArray;
    set @r, 0;
    set @j, 0;
    goto L_clothes_choice2;

L_clothes_choice2:
    mes "[Salmi]";
    mes "\"What assassin item can I dye for you today?\"";
    next;
    callfunc "DynamicItemMenu$";
    set @default_choice$, "";
    set @item_del$, @item$;
    set @name$, @item_names$[@index];
    cleararray @items$, "", getarraysize(@items$);
    cleararray @item_names$, "", getarraysize(@item_names$);
    if(@item$ == "") goto L_Close;
    goto L_picked_item;




L_picked_item:
    if (countitem(@item_del$) == 0) goto L_havenone;
    goto L_colour;

L_colour:
    mes "[Salmi]";
    mes "\"Excellent. Now, what color do you want?\"";
    next;
    set @r, 0;
    set @j, getarraysize($@DYE_assassin_colors$);
    cleararray @items$, "", getarraysize(@items$);
    cleararray @item_names$, "", getarraysize(@item_names$);
    callsub S_CloneColors;
    set @default_choice$, "I have no dye.";
    callfunc "DynamicItemMenu$";
    cleararray @items$, "", getarraysize(@items$);
    cleararray @item_names$, "", getarraysize(@item_names$);
    set @default_choice$, "";
    if (@item$ == "") goto L_no_dye;
    set @colour, @index;
    if ($@DYE_assassin_color_names$[@colour] == "Black") goto L_no_posible;
    goto L_finish;

S_CloneColors:
    set @items$[@r],      $@DYE_assassin_colors$[@r] + "Dye";
    set @item_names$[@r], $@DYE_assassin_color_names$[@r];
    set @r, @r + 1;
    if(@r < @j) goto S_CloneColors;
    set @r, 0;
    set @j, 0;
    return;

L_no_dye:
    mes "[Salmi]";
    mes "\"I would love to dye your " + @name$ + " for you, but you will have to bring me some dye first.";
    mes "Those behind me are nearly empty and won't last for your item.\"";
    next;
    menu
        "I wanted to dye a different item anyway.", L_clothes_choice,
        "What do you mean, `bring you dye'?", L_explain_dye,
        "Where would I get dye?", L_explain_dye,
        "Never mind.", L_Close;

L_explain_dye:
    if (QUEST_clothdyer_knowsdye < 1)
        set QUEST_clothdyer_knowsdye, 1;
    mes "[Salmi]";
    mes "\"Well, dye is very expensive, and since I don't charge adventurers anything, I can't give you any for free.";
    mes "But most alchemists can make dye; perhaps you can find one around here.\"";
    goto L_Close;

L_finish:
    if(getitemlink($@DYE_assassin_colors$[@colour] + @item_del$) == "Unknown Item") mapexit;
    if(countitem(@item_del$) < 1) goto L_havenone;
    delitem @item$, 1;
    delitem @item_del$, 1;
    getitem $@DYE_assassin_colors$[@colour] + @item_del$, 1;
    mes "[Salmi]";
    mes "\"Here's your " + $@DYE_assassin_color_names$[@colour] + " " + @name$ + "!  Please let dry for a minute.\"";
    goto L_Close;

L_no_posible:
    mes "[Salmi]";
    mes "That colored assassin item does not exists!";
    next;
    menu
        "I wanted to dye a different colored item anyway.", L_clothes_choice,
        "Never mind.", L_Close;

L_havenone:
    mes "[Salmi]";
    mes "\"You don't have any uncolored " + @name$ + "?  That's unfortunate.";
    mes "Would you like to dye something else?\"";
    next;
    goto L_clothes_choice;

L_Close:
    mes "\"Farewell and good luck in your journeys!\"";
    close2;
    emotion EMOTE_WINK, strcharinfo(0);
    end;

L_Close3:
    mes "\"You does not seems a real Assassin and me only dye for my brothers!!!\"";
    mes "\"Go out and die by my Assassins army... Muahahahahah!!!\"";
    close2;
    emotion EMOTE_DISGUST, strcharinfo(0);
    warp "068-1", 126, 69;
    end;
}
